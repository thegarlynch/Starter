plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "kotlin-kapt"
    id "dagger.hilt.android.plugin"
}

apply from: "$rootDir/script/spotless.gradle"

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.thegar.starter"
        targetSdk 32
        versionCode 1
        versionName "1.0"

        missingDimensionStrategy "minApi", "minApi19", "minApi21", "minApi23", "minApi24"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    flavorDimensions "version", "minApi"
    productFlavors {
        development {
            dimension "version"
        }

        production {
            dimension "version"
        }

        minApi19 {
            dimension "minApi"
            minSdk 19
            multiDexEnabled true
        }

        minApi21 {
            dimension "minApi"
            minSdk 21
        }
    }

    variantFilter { variant ->
        def names = variant.flavors*.name
        def isReleaseBuildType = variant.buildType.name == "release"
        if(names.contains("minApi21") && isReleaseBuildType) {
            setIgnore(true)
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
        freeCompilerArgs += "-opt-in=kotlin.ExperimentalStdlibApi"
    }

    kapt {
        correctErrorTypes true
    }

}

dependencies {
    implementation project(":compat")

    // Retrofit
    implementation libs.retrofit

    // Android ThreeTen Backport
    implementation libs.threeten

    // Dagger Hilt
    implementation libs.hilt.android
    kapt libs.hilt.compiler

    testImplementation libs.hilt.android.testing
    kaptTest libs.hilt.compiler

    androidTestImplementation libs.hilt.android.testing
    kaptAndroidTest libs.hilt.compiler

    // Timber
    implementation libs.timber

    // moshi-kotlin would depend on 2.5MB of kotlin-reflect library
    // https://github.com/square/moshi#reflection
    debugImplementation libs.moshi.debug
    releaseImplementation libs.moshi.release
    kaptRelease libs.moshi.kapt

    // Lifecycle
    implementation libs.bundles.lifecycle

    // OkHttp Interceptor
    implementation libs.okhttp.logging

    // Room
    implementation libs.room.runtime
    kapt libs.room.compiler

    implementation "androidx.core:core-ktx:1.8.0"
    implementation "androidx.appcompat:appcompat:1.4.2"
    implementation "com.google.android.material:material:1.6.1"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
}