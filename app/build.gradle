plugins {
    id "com.android.application"
    id "org.jetbrains.kotlin.android"
    id "kotlin-kapt"
    id "dagger.hilt.android.plugin"
}

apply from: "$rootDir/script/spotless.gradle"

android {
    compileSdk 32

    defaultConfig {
        applicationId "com.thegar.starter"
        minSdk 19
        targetSdk 32
        versionCode 1
        versionName "1.0"

        missingDimensionStrategy "minApi", "minApi19", "minApi21", "minApi23", "minApi24"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
        }

        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile("proguard-android-optimize.txt"), "proguard-rules.pro"
        }
    }

    flavorDimensions "version", "minApi"
    productFlavors {
        development {
            dimension "version"
        }

        production {
            dimension "version"
        }

        minApi19 {
            dimension "minApi"
            minSdk 19
            multiDexEnabled true
        }

        minApi21 {
            dimension "minApi"
            minSdk 21
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = "11"
        freeCompilerArgs += "-opt-in=kotlin.ExperimentalStdlibApi"
    }

    kapt {
        correctErrorTypes true
    }

}

dependencies {
    implementation project(":compat")

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:2.9.0"

    // Android ThreeTen Backport
    implementation "com.jakewharton.threetenabp:threetenabp:1.4.0"

    // Dagger Hilt
    implementation "com.google.dagger:hilt-android:$hilt_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_version"

    testImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    kaptTest "com.google.dagger:hilt-compiler:$hilt_version"

    androidTestImplementation "com.google.dagger:hilt-android-testing:$hilt_version"
    kaptAndroidTest "com.google.dagger:hilt-compiler:$hilt_version"

    // Timber
    implementation "com.jakewharton.timber:timber:5.0.1"

    // moshi-kotlin would depend on 2.5MB of kotlin-reflect library
    // https://github.com/square/moshi#reflection
    debugImplementation moshiDebug
    releaseImplementation moshiRelease
    kaptRelease moshiKapt

    def lifecycle_version = "2.5.0"

    // Lifecycle
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-common-java8:$lifecycle_version"

    implementation "androidx.core:core-ktx:1.8.0"
    implementation "androidx.appcompat:appcompat:1.4.2"
    implementation "com.google.android.material:material:1.6.1"
    implementation "androidx.constraintlayout:constraintlayout:2.1.4"
    testImplementation "junit:junit:4.13.2"
    androidTestImplementation "androidx.test.ext:junit:1.1.3"
    androidTestImplementation "androidx.test.espresso:espresso-core:3.4.0"
}