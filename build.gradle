// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {

    ext {
        hilt_version = "2.42"

        def moshi_version = "1.13.0"

        // moshi-kotlin would depend on 2.5MB of kotlin-reflect library
        // https://github.com/square/moshi#reflection
        moshiDebug = "com.squareup.moshi:moshi-kotlin:$moshi_version"
        moshiRelease = "com.squareup.moshi:moshi:$moshi_version"
        moshiKapt = "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"
    }

    dependencies {
        // other plugins...
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_version"
    }
}

plugins {
    id "com.android.application" version "7.2.0" apply false
    id "com.android.library" version "7.2.0" apply false
    id "org.jetbrains.kotlin.android" version "1.7.10" apply false
    id "org.gradle.android.cache-fix" version "2.5.6" apply false
    id "com.osacky.doctor" version "0.8.1"
    id "com.diffplug.spotless" version "6.8.0" apply false
}

task createSpotlessPreCommitHook() {
    doLast {
        def hookDir = new File("${project.rootDir}/.git/hooks")
        if (!hookDir.exists()) {
            hookDir.mkdirs()
        }

        def file = new File("${project.rootDir}/.git/hooks", "pre-commit")
        if (!file.exists()) {
            "cp ${project.rootDir}/script/pre-commit-hook ${project.rootDir}/.git/hooks/pre-commit".execute()
            "chmod +x ${project.rootDir}/.git/hooks/pre-commit".execute()
        }
    }
}